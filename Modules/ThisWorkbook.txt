VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ThisWorkbook"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
'*************************************************************************************************
'
'   ThisWorkbook
'       1. RibbonCommands calls the public subs
'                populateJobHeaders() and populateReport()
'       2. From there this call the private subs
'               populateHeaderFeatureInfo(), populateFeatureMeasuredValues(), populateTraceabilityInfo()
'*************************************************************************************************



Sub populateJobHeaders(jobNum As String, routine As Variant, customer As Variant, machine As Variant, partNum As Variant, rev As Variant, partDesc As Variant)
    On Error GoTo PopHeadersErr:
    Call Cleanup

    'If somebody entered nonsense into the Routine ComboBox, then we clear our variables and call this sub
    'That way the cleanup subroutine happens but then we skip setting empty values
    If IsEmpty(routine) Then Exit Sub


    With Worksheets("PAGE 1")
        .Unprotect Password:="3063bqa"

        .Range("C1").Value = jobNum
        .Range("C2").Value = routine
        .Range("C3").Value = customer
        .Range("C4").Value = machine
        .Range("G3").Value = partNum
        .Range("I3").Value = rev
        .Range("G4").Value = partDesc


        .Protect Password:="3063bqa", AllowFormattingCells:=True
    End With
    Exit Sub

PopHeadersErr:
    Err.Raise Number:=Err.Number, Description:="Sub: ThisWorkbook.populateJobHeaders" & vbCrLf & Err.Description

End Sub

Public Sub populateReport(header_info As Collection, insp_data As Collection, traceability As Collection)
    'Called by RibbonCommands, see HTTPConnections for data format
    
    If header_info Is Nothing Or insp_data Is Nothing Then Exit Sub
    Application.ScreenUpdating = False
    
    Dim applicablePages As Integer
    applicablePages = Application.WorksheetFunction.Floor(header_info.Count / 8, 1) + 1
    
    Application.ScreenUpdating = False
    
    If applicablePages > DataSources.AVAILABLE_PAGES Then
        Err.Raise Number:=vbObjectError + 9999, Description:="Not enough room to fit all the dimensions"
    End If
    
    For i = 1 To applicablePages
        ThisWorkbook.Worksheets("PAGE " & i).Unprotect Password:="3063bqa"
        ThisWorkbook.Worksheets("PAGE " & i).Visible = True
    Next i
    
    Call populateHeaderFeatureInfo(header_info:=header_info)
    Call populateFeatureMeasuredValues(insp_data:=insp_data, header_info:=header_info)
    Call populateTraceabilityInfo(run_traceability_json:=traceability, pages:=applicablePages)
    
    'Relock the sheets
    For i = 1 To applicablePages
        ThisWorkbook.Worksheets("PAGE " & i).Protect Password:="3063bqa"
    Next i

    Application.ScreenUpdating = True
    
End Sub


Public Sub populateAttrSheet(attFeatHeaders() As Variant, attFeatResults() As Variant, attFeatTraceability() As Variant, _
                                noResults As Boolean, noTraceability As Boolean, noVariables As Boolean)
    'attFeatHeaders(0,i) -> Balloon Num
    'attFeatHeaders(1,i) -> Description
    'attFeatHeaders(2,i) -> Attr Tolerance
    'attFeatHeaders(3,i) -> Frequency
    'attFeatHeaders(4,i) -> Inspection Method
    'attFeatHeaders(5,i) -> Feature Name
    
    'attFeatResults(0,0) -> 0 [Pass] / 1 [Fail]
    'attFeatResults(1,0) -> Feature Count
    
    'attFeatTraceability(0,i) -> Feature Name
    'attFeatTraceability(1,i) -> Employee Num
    'attFeatTraceability(2,i) -> DateTime
    
    
    
    On Error GoTo attSheetErr:
    If (Not attFeatHeaders) = -1 Or (Not attFeatTraceability) = -1 Then Exit Sub
    If RibbonCommands.samplingSize = vbNullString Then
        Call SetAQL(customer:=RibbonCommands.customer, drawNum:=RibbonCommands.drawNum, _
                ProdQty:=RibbonCommands.ProdQty, IsChildOrParent:=(RibbonCommands.IsParentJob Or RibbonCommands.IsChildJob))
    End If
   
    With Sheets("PAGE_Attr")
        .Visible = True
        .Unprotect Password:="3063bqa"
        
        Dim i As Integer
        For i = 0 To UBound(attFeatHeaders, 2)
            .Range("A" & 11 + i).Value = attFeatHeaders(0, i) 'Balloon
            .Range("B" & 11 + i).Value = attFeatHeaders(1, i) 'Char. Description
            .Range("E" & 11 + i).Value = attFeatHeaders(2, i) 'Tolerance
            
            If RibbonCommands.samplingSize >= RibbonCommands.ProdQty Then
                .Range("H" & 11 + i).Value = format(RibbonCommands.custAQL, "0.00") ''AQL
                .Range("I" & 11 + i).Value = "100%" 'SamplingSize
            Else
                .Range("H" & 11 + i).Value = format(RibbonCommands.custAQL, "0.00") 'AQL
                .Range("I" & 11 + i).Value = RibbonCommands.samplingSize 'SamplingSize
            End If
            
            .Range("G" & 11 + i).Value = ExcelHelpers.GetShortHandMethod(attFeatHeaders(4, i)) 'Inspection Method
        
            If noResults Then
                .Range("J" & 11 + i).Value = "Fail" 'Result
                .Range("J" & 11 + i).Interior.Color = RGB(255, 102, 102)
            Else
                .Range("J" & 11 + i).Value = "Pass" 'Result
            End If
            
            If noTraceability Then
                .Range("K" & 11 + i).Interior.Color = RGB(255, 102, 102) 'EmployeeID
                .Range("L" & 11 + i).Interior.Color = RGB(255, 102, 102) 'Date
            Else
                .Range("K" & 11 + i).Value = attFeatTraceability(1, i) 'EmployeeID
                .Range("L" & 11 + i).Value = attFeatTraceability(2, i) 'Date
            End If
        Next i
        
        .Protect Password:="3063bqa"
    End With
    
    If noVariables Then Worksheets("PAGE 1").Visible = False
    Exit Sub
    
attSheetErr:
    With Sheets("PAGE_Attr")
        .Visible = False
        .Protect Password:="3063bqa"
    
    End With
    
    Err.Raise Number:=Err.Number, Description:="Sub: ThisWorkbook.populateAttrSheet" & vbCrLf & Err.Description

End Sub


Private Sub populateHeaderFeatureInfo(header_info As Collection)
        'Maybe, it could be 1-indexed
    'featureInfo(0,i) -> Balloon Number
    'featureInfo(1,i) -> Feature Description
    'featureInfo(2,i) -> Lower Tolerance
    'featureInfo(3,i) -> Target
    'featureInfo(4,i) -> Upper Tolerance
    'featureInfo(5,i) -> Gauge ID
    'featureInfo(6,i) -> Variable / Attribute
    'featureInfo(7,i) -> Attribute Tolerance
    
    On Error GoTo FeatureHeadersErr
    
    
    For i = 1 To header_info.Count
            Dim activePageNum As Integer
            'Each worksheet can only hold 8 features at a time, beyond that we need to switch the active page we're putting info on
            activePageNum = Application.WorksheetFunction.Floor((i - 1) / 8, 1) + 1
            
            With ThisWorkbook.Worksheets("PAGE " & activePageNum)
                Dim colLetter As String
                colLetter = ExcelHelpers.GetAddress(((i - 1) Mod 8) + 5)  'The first feature colum starts 5 columns away

'TODO: we are assuming that the custom fields always appear in Id index order, but we should explicity sort this maybe

                .Range(colLetter & "9").Value = header_info(i)("name") 'Balloon
                .Range(colLetter & "10").Value = header_info(i)("custom_fields")(7)("value") 'Description
                
                If header_info(i)("type") = DataSources.TYPE_VARIABLE Then
                    .Range(colLetter & "16").Value = header_info(i)("properties")("ltol") 'LTol
                    .Range(colLetter & "17").Value = header_info(i)("properties")("nominal") 'Target
                    .Range(colLetter & "18").Value = header_info(i)("properties")("utol") 'UTol
                Else
                    'TODO, if the featureInfo(7,i) <> "NA" then  we should attempt to evaluate it and split it
                        ' as well as find the target value. will need to find the sig digs
                        'and the boolean of a leading digit for this.
                    If header_info(i)("custom_fields")(1)("value") = "NA" Then
                        .Range(colLetter & "16").Value = "N/A" 'LTol
                        .Range(colLetter & "17").Value = "N/A" 'Target
                        .Range(colLetter & "18").Value = "N/A" 'UTol
                    Else
                        Dim attTol() As Variant
                        attTol = SplitAttrTol(header_info(i)("custom_fields")(1)("value"))
                        .Range(colLetter & "16").Value = attTol(0) 'LTol
                        .Range(colLetter & "17").Value = attTol(1) 'Target
                        .Range(colLetter & "18").Value = attTol(2) 'UTol
                    End If
                End If
                
                .Range(colLetter & "19").Value = ExcelHelpers.GetShortHandMethod(header_info(i)("custom_fields")(3)("value")) 'Inpsection Method
                
                If header_info(i)("type") = DataSources.TYPE_VARIABLE Then 'Variable / Attribute  (hidden cell for later reference)
                    .Range(colLetter & "8").Value = "Variable"
                Else
                    .Range(colLetter & "8").Value = "Attribute"
                End If
            End With
    Next i
    
    Exit Sub
    
FeatureHeadersErr:
    ExcelHelpers.CloseDataValWB
    Err.Raise Number:=Err.Number, Description:="Sub: ThisWorkbook.populateHeaderFeatureInfo" & vbCrLf & Err.Description
End Sub


Private Sub populateFeatureMeasuredValues(insp_data As Collection, header_info As Collection)
    'see HTTPConnections.GetPassedInspData   for data format
    
    On Error GoTo FeatureValuesErr
    
    If insp_data Is Nothing Then Exit Sub
    
    
'    {
'            "ObsNo": 1,
'            "(i)": 0.0,
'            "(ii)": 0.0,
'            "(iii)": 0.0,
'            "0_006_00": 0.0,
'            "0_010_00": 0.0,
'            "0_012_00": 0.1852,
'            "0_024_00": 0.0
'        },
    
    For i = 1 To insp_data.Count
        For j = 1 To header_info.Count  'for each row, use the feature name as the key to get its measured value
            activePageNum = Application.WorksheetFunction.Floor((j - 1) / 8, 1) + 1
            With ThisWorkbook.Worksheets("PAGE " & activePageNum)
                colLetter = ExcelHelpers.GetAddress(((j - 1) Mod 8) + 5)
                
'TODO: determing teh feature Type here
                Dim insp_value As Variant
                insp_value = insp_data(i)(header_info(j)("name"))
                '.Range(colLetter & (i - 1) + 22).Value = insp_data(i)(header_info(j)("name"))
                
                If .Range(colLetter & "8").Value = "Attribute" Then
                    If insp_value = 0 Then
                        .Range(colLetter & (i - 1) + 22).Value = "Pass"
                    Else
                        .Range(colLetter & (i - 1) + 22).Value = "Fail"
                    End If
                Else
                    'Otherwise its variable
                    If insp_value = DataSources.VARIABLE_FAILED_VALUE Then
                        'ShowALLObs needs to differentiate Failed measurements from missing ones and does this through a special value
                        .Range(colLetter & (i - 1) + 22).Value = "Fail"
                    Else
                        'Set the good value
                        .Range(colLetter & (i - 1) + 22).Value = insp_value
                    End If

                End If
            End With
NextMeasureMent:
        Next j
    Next i
    

    Exit Sub
    
FeatureValuesErr:
    Err.Raise Number:=Err.Number, Description:="Sub: ThisWorkbook.populateFeatureMeasuredValues" & vbCrLf & Err.Description

End Sub

Private Sub populateTraceabilityInfo(run_traceability_json As Collection, pages As Integer)
    'Called By populateReport()
    'See HTTPConnections.GetPasssedData() for more info on Data Format
    
    '[
    '        {
    '            "TimeStamp": "2021-04-19T20:53:29.183000",
    '            "EmployeeID": "0389",
    '            "ObsNo": 1,
    '            "Result": "Pass"
    '        }, ]

    On Error GoTo FeatureTraceErr

    'For each page, we add a copy of our traceability information
    Dim trace_info As Dictionary
    For i = 1 To pages
        With Me.Worksheets("PAGE " & i)
            For j = 1 To run_traceability_json.Count
                Dim normalized_timestamp As String
                normalized_timestamp = Replace(Replace(run_traceability_json(j)("TimeStamp"), "T", " "), "Z", "")
                .Range("A" & j + 21).Value = normalized_timestamp
                
                Dim emp_info As Variant, emp_cell As Range
                emp_info = run_traceability_json(j)("EmployeeID")
                Set emp_cell = .Range("C" & j + 21)
                
                If IsNull(emp_info) Then
                    emp_cell.Value = emp_cell.Offset(-1, 0).Value
                Else
                    emp_cell = emp_info
                End If
                
                    'Turn failed inspections Red for visibility
                If run_traceability_json(j)("Result") = "Fail" Then
                    .Range("A" & j + 21 & ":" & "L" & j + 21).Interior.Color = RGB(255, 0, 0)
                End If
            Next j
        End With
    Next i

    Exit Sub


FeatureTraceErr:
    Err.Raise Number:=Err.Number, Description:="Sub: ThisWorkbook.populateTraceabilityInfo" & vbCrLf & Err.Description

End Sub


Public Sub Cleanup()

    Application.ScreenUpdating = False
    
    'Reset the Header information on Page 1
    With Worksheets("PAGE 1")
        .Unprotect Password:="3063bqa"
        
        .PageSetup.RightHeader = vbNullString

        .Range("C1").Value = ""
        .Range("C2").Value = ""
        .Range("C3").Value = ""
        .Range("C4").Value = ""
        .Range("G3").Value = ""
        .Range("I3").Value = ""
        .Range("G4").Value = ""


        .Protect Password:="3063bqa", AllowFormattingCells:=True
        .Visible = True

    End With

    'Reset everything else on all sheets
    For i = 1 To DataSources.AVAILABLE_PAGES
        With ThisWorkbook.Worksheets("PAGE " & i)
            If .Visible = True Then
                .Activate
                .Unprotect Password:="3063bqa"
                
                .PageSetup.RightHeader = vbNullString
            
                'Clean Data/Employee Information
                With Range("A22:C121")
                    .Value = ""
                    .Interior.Color = RGB(255, 255, 255)
                    .Font.Color = RGB(0, 0, 0)
                End With
                
                'Clean measured values
                With Range("E22:L121")
                    .Value = ""
                    .Interior.Color = RGB(255, 255, 255)
                    .Font.Color = RGB(0, 0, 0)
                End With
                
                'Clean header information
                With Range("E8:L19")
                    .Value = ""
                    .Interior.Color = RGB(255, 255, 255)
                    .Font.Color = RGB(0, 0, 0)
                End With
                
                .Protect Password:="3063bqa"
                
                'Leave only the first page visible
                If .Name <> "PAGE 1" Then
                    .Visible = False
                End If
            End If
            
        End With
    Next i
    
    'Reset the PAGE_Attr sheet
    With Worksheets("PAGE_Attr")
        .Unprotect Password:="3063bqa"
        
        .PageSetup.RightHeader = vbNullString
    
        With .Range("A11:L117")
            .Value = ""
            .Interior.Color = RGB(255, 255, 255)
        End With
    
        .Visible = False
        .Protect Password:="3063bqa"
    End With
    
    ThisWorkbook.Worksheets("PAGE 1").Activate
    
    Application.ScreenUpdating = True
    
'TODO: just have this here for testing, remove later
    Me.Worksheets("PAGE 1").Unprotect "3063bqa"

End Sub



'****************************************************************************************
'               Extra Functions
'****************************************************************************************


Private Function SplitAttrTol(inpAttrTol As Variant, Optional featureName As String) As Variant()
    Dim attrTol As String
    Dim tolRange() As String
    Dim numDec As Integer
    Dim hasLeadingChar As Boolean
    Dim outFormat As String
    
    On Error GoTo SplitAttrErr
    
    attrTol = Replace(inpAttrTol, " ", "") 'Repalce whitespace
    
    If UCase(inpAttrTol) Like "*MAX*" Then
        inpAttrTol = Replace(inpAttrTol, "MAX", "")
        SplitAttrTol = Array("N/A", "N/A", inpAttrTol)
        Exit Function
    ElseIf UCase(inpAttrTol) Like "*MIN*" Then
        inpAttrTol = Replace(inpAttrTol, "MIN", "")
        SplitAttrTol = Array(inpAttrTol, "N/A", "N/A")
        Exit Function
    End If
    
    
    Dim i As Integer
        'Walk through and detect
    For i = 1 To Len(attrTol)
        Dim char As String
        char = Mid(attrTol, i, 1)
        If Not (IsNumeric(char) Or char = "." Or char = "/") Then
            'Its something that we cant process, so lets just return "NA"s
            SplitAttrTol = Array("N/A", "N/A", "N/A")
            Exit Function
        End If
    Next i
    
    tolRange = Split(attrTol, "/")
    
    numDec = Application.WorksheetFunction.Max(GetDecimalPlaces(tolRange(0), hasLeadingChar), GetDecimalPlaces(tolRange(1), hasLeadingChar))
    outFormat = GenFormat(numDec, hasLeadingChar)
    
    'Maybe another error handling here
    Dim calcTarget As Double
    Dim outStr As String
    calcTarget = Application.WorksheetFunction.Round((CDbl(tolRange(0)) + CDbl(tolRange(1))) / 2, numDec)
    
        'If the avg is the same as either Tolerance bound because both are only a single unit apart of the lowest significant digit
            'Then extend the format out another sigDig to show the difference
    If (CStr(calcTarget) = tolRange(0) Or CStr(calcTarget) = tolRange(1)) And InStr(outFormat, ".") > 0 Then
        calcTarget = Application.WorksheetFunction.Round((CDbl(tolRange(0)) + CDbl(tolRange(1))) / 2, numDec + 1)
        outFormat = outFormat & "0"
    End If
    outStr = format(CStr(calcTarget), outFormat)
    
    SplitAttrTol = Array(tolRange(0), outStr, tolRange(1))

    Exit Function
    
SplitAttrErr:
    'Just return the default values, we couldn't parse here
    SplitAttrTol = Array("N/A", "N/A", "N/A")
    
    'TODO: Dont want to interrupt the current user with a msg, especially since this would happen during printing.
    'We should setup some kind of logging service that will keep track, passing in the inpAttrTol, the featureName and
        'Getting the RibbonCommands.jobNumUCase , customer and partNum as well. From there it can be narrowed down
End Function


Private Function GetDecimalPlaces(val As String, ByRef leadingChar As Boolean) As Integer
    Dim location As Integer
    location = InStr(val, ".")
    If location = 0 Then
        GetDecimalPlaces = 0
        Exit Function
    End If
        'If the "." is not the first character then we have leading characters in this value
    If location > 1 Then leadingChar = True

    GetDecimalPlaces = Len(val) - location
End Function


Private Function GenFormat(sigDig As Integer, hasLeadingChar As Boolean) As String
    Dim format As String
    If hasLeadingChar Then
        format = "0"
    Else
        format = "#"
    End If
    
    If sigDig = 0 Then
        GenFormat = format
        Exit Function
    End If
    
    format = format & "."
    
    Dim i As Integer
    For i = 1 To sigDig
        format = format & "0"
    Next i
    
    GenFormat = format
End Function

Sub test_Header()
    Worksheets("PAGE 1").PageSetup.RightHeader = vbCrLf & "Page &P of " & Worksheets("PAGE 1").Range("K4").Value



End Sub

Public Sub PrintResults()

    Dim endPage As Integer
    'we set a hidden value at this cell to tell us how many pages needed to print
    endPage = CInt(Worksheets("PAGE 1").Range("L4").Value)
    
    
    For Each Sheet In Worksheets
        'Defaults to printing to the ActivePrinter which the user can choose to set in the Settings tab
        If Sheet.Visible = True Then
            If Sheet.Name = "PAGE_Attr" Then
                'The attr sheet increments pages by featureCount, not observation count
                Dim attEnd As Integer
                attEnd = CInt(Sheet.Range("L4").Value)
                Sheet.PageSetup.RightHeader = vbCrLf & "Page &P of " & attEnd
                Sheet.Printout From:=1, To:=attEnd, Collate:=True
            Else
                Sheet.PageSetup.RightHeader = vbCrLf & "Page &P of " & endPage
                Sheet.Printout From:=1, To:=endPage, Collate:=True
            End If
        End If
    Next Sheet


End Sub



Private Sub Workbook_BeforeClose(Cancel As Boolean)
        On Error GoTo CloseWB
        Call DatabaseModule.Close_Connections
CloseWB:

End Sub

    'Wrapper for ExcelHelpers.GetAQL()
    'Likely only called in the event the user does not have "Auto Load Checklist" enabled
Private Sub SetAQL(customer As String, drawNum As String, ProdQty As Integer, IsChildOrParent As Boolean)
    Dim aqlValues() As String, childOrParent As Boolean
    childOrParent = (RibbonCommands.IsChildJob Or RibbonCommands.IsParentJob)
    
    aqlValues = ExcelHelpers.GetAQL(customer:=RibbonCommands.customer, drawNum:=RibbonCommands.drawNum, _
                                            ProdQty:=RibbonCommands.ProdQty, isChildOrParentJob:=childOrParent)
    
    RibbonCommands.samplingSize = aqlValues(0)
    RibbonCommands.custAQL = aqlValues(1)
    
    If childOrParent Then
        RibbonCommands.parentChildSamplingSize = aqlValues(2)
        RibbonCommands.parentChildFinalAQL = aqlValues(3)
    End If
        
End Sub



Sub TEST_set_features()
    Dim jobNumUcase As String, rtCombo_TextField As String, result As Dictionary, run_feature_json As Collection, run_data_json As Collection, run_traceability_json As Collection

    jobNumUcase = "SD1419"
    rtCombo_TextField = "DRW-00815-01_RAG_IP_BENCH"
    ExcelHelpers.OpenDataValWB
    
    Set result = HTTPconnections.GetPassedInspData(jobNumUcase, rtCombo_TextField)
    Set run_feature_json = result("feature_info")
    Set run_data_json = result("insp_data")
    Set run_traceability_json = result("traceability")

    populateHeaderFeatureInfo header_info:=run_feature_json
    populateFeatureMeasuredValues insp_data:=run_data_json, header_info:=run_feature_json
    populateTraceabilityInfo run_traceability_json, 1
    
            
    ExcelHelpers.CloseDataValWB
    Exit Sub
wbErr:
    ExcelHelpers.CloseDataValWB
    MsgBox "Error Occurred at Sub: RibbonCommands.SetWorkbookInformation", vbCritical
    Err.Raise Number:=vbObjectError + 1200
    
End Sub

